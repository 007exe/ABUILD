pkgname=libupnp
pkgver=1.14.16
pkgbuild=1
arch=("auto")

shortdesc=("Portable Open Source UPnP Development Kit.")

source=("https://github.com/pupnp/pupnp/archive/release-${pkgver}.tar.gz")

tags=("libs net-libs")

adddep=("glibc")

build_deps=("${adddep} cmake")

# NOTE: Удаляем исходники пакета после сборки
# FIXME: mkpkg не умеет сохранять скачанные файлы под новыми именами, что может приводить к
# ошибкам когда в кеше имеется архив с исходниками от другого пакета с тем же именем
before_build(){
  go_src_dir
  rm -f ${srcache}/release-${pkgver}.tar.gz
}

build() {
  go_src_dir
  burn_patches
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=None \
    -DUPNP_BUILD_STATIC=OFF \
    -Dblocking_tcp_connections=OFF \
    -Dreuseaddr=ON \
    -Wno-dev
  make VERBOSE=1 -C build
  make VERBOSE=1 DESTDIR=${pkgdir} install -C build
  install -vDm 644 COPYING -t ${pkgdir}/usr/share/licenses/${pkgname}
  install -vDm 644 {ChangeLog,README.md} -t ${pkgdir}/usr/share/doc/${pkgname}
}
