pkgname=gtest
pkgver=1.12.1
pkgbuild=1
arch=("auto")

shortdesc=("Google Test - C++ testing utility.")
longdesc=("GoogleTest now follows the Abseil Live at Head philosophy. We recommend using the latest commit in the main branch in your projects.")

source=("https://github.com/google/googletest/archive/refs/tags/release-${pkgver}.tar.gz")

tags=("libs dev-cpp")

build_deps=("python cmake gcc-libs")

adddep=("bash")

before_build(){
  cd ${startdir}/patches
  sed "s|__GOOGLETEST_VERSION__|${pkgver}|g" -i gtest-version.patch
  go_src_dir
  burn_patches
}

build() {
  go_src_dir
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON \
    -Dgtest_build_tests=ON
  cmake --build build -j${numjobs}
  DESTDIR=${pkgdir} cmake --build build --target install

  find ${pkgdir} -name '*.pump' -printf 'Removing %P\n' -delete

  install -Dm 644 LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}
  install -Dm 644 README.md CONTRIBUTORS -t ${pkgdir}/usr/share/doc/${pkgname}

  cd googletest
  install -Dm 644 cmake/* -t ${pkgdir}/usr/src/googletest/cmake
  install -Dm 644 src/* -t ${pkgdir}/usr/src/googletest/src
  install -Dm 644 CMakeLists.txt -t ${pkgdir}/usr/src/googletest

  cd ../googlemock
  install -Dm 644 cmake/* -t ${pkgdir}/usr/src/gmock/cmake
  install -Dm 644 src/* -t ${pkgdir}/usr/src/gmock/src
  install -Dm 644 CMakeLists.txt -t ${pkgdir}/usr/src/gmock

  sed -i 's|src/||' ${pkgdir}/usr/src/gmock/src/gmock-all.cc

  python -m compileall -d /usr/share/gmock ${pkgdir}/usr/share/gmock
  python -O -m compileall -d /usr/share/gmock ${pkgdir}/usr/share/gmock

}
